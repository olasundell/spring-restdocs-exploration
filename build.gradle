buildscript {
	ext {
		springBootVersion = '1.5.1.RELEASE'
	}
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
	}
}

plugins {
	id "org.asciidoctor.convert" version "1.5.2"
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'

jar {
	baseName = 'spring-rest-docs'
	version = '0.0.1-SNAPSHOT'
}

sourceCompatibility = 1.8

repositories {
	mavenCentral()
}
ext {
	snippetsDir = file('build/generated-snippets')
}

test {
	outputs.dir snippetsDir
}

asciidoctor {
	attributes 'snippets': snippetsDir
	inputs.dir snippetsDir
	dependsOn test
}

jar {
	dependsOn asciidoctor
	from ("${asciidoctor.outputDir}/html5") {
		into 'static/docs'
	}
}

task copyRestDocs(type: Copy) {
	dependsOn asciidoctor
	from "${asciidoctor.outputDir}/html5"
	into "${sourceSets.main.output.resourcesDir}/static/docs"
}

bootRun {
	dependsOn copyRestDocs
}

dependencies {
	compileOnly('org.projectlombok:lombok')
	compile('org.springframework.boot:spring-boot-starter-web')
	compile "io.javaslang:javaslang:2.0.5"
	compile("io.javaslang:javaslang-jackson:2.0.5")

	testCompile('org.springframework.boot:spring-boot-starter-test')
	testCompile('org.springframework.restdocs:spring-restdocs-restassured')
//	testCompile(group: 'io.rest-assured', name: 'rest-assured', version: '3.0.2')
//	testCompile 'org.springframework.restdocs:spring-restdocs-mockmvc:1.1.2.RELEASE'
}
